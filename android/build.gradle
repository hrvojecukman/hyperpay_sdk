group 'com.hyperpay.sdk'
version '7.4.0'

buildscript {
    ext.kotlin_version = '2.0.21'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace 'com.hyperpay.sdk'
    compileSdk 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdk 24
        targetSdk 35
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    // HyperPay OPPWA SDK (via JitPack)
    api 'com.github.hrvojecukman.hyperpay_sdk_android:oppwa:v7.4.0.1:oppwa@aar'

    // 3DS2 SDK â€” debug vs release variants (via JitPack)
    debugApi 'com.github.hrvojecukman.hyperpay_sdk_android:ipworks3ds:v7.4.0.1:ipworks3ds@aar'
    releaseApi 'com.github.hrvojecukman.hyperpay_sdk_android:ipworks3ds_deploy:v7.4.0.1:ipworks3ds_deploy@aar'

    // AndroidX & Google dependencies required by OPPWA SDK
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'androidx.webkit:webkit:1.12.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'androidx.fragment:fragment-ktx:1.8.6'
    implementation 'com.google.android.gms:play-services-wallet:19.4.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
